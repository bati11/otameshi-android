plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.protobuf'
}

dependencies {
    implementation(libs.kotlinx.coroutines.android)

    // gRPC
    implementation(libs.grpc.stub)
    implementation(libs.grpc.protobuf.lite)
    implementation(libs.grpc.kotlin.stub)
    implementation(libs.protbuf.kotlin.lite)
}

android {
    compileSdk = rootProject.ext.compileSdk
    buildToolsVersion = "32.0.0"

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs = ["-opt-in=kotlin.RequiresOptIn"]
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${libs.versions.protobuf.version.get()}"
    }
    plugins {
        java {
            artifact = "io.grpc:protoc-gen-grpc-java:${libs.versions.grpc.version.get()}"
        }
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${libs.versions.grpc.version.get()}"
        }
        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:${libs.versions.grpc.kotlin.version.get()}:jdk7@jar"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                kotlin { option 'lite' }
            }
            task.plugins {
                java { option 'lite' }
                grpc { option 'lite' }
                grpckt { option 'lite' }
            }
        }
    }
}
