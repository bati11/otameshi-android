plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.protobuf'
}

dependencies {
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4")

    // gRPC
    implementation("io.grpc:grpc-stub:$grpc_version")
    implementation("io.grpc:grpc-protobuf-lite:$grpc_version")
    implementation("io.grpc:grpc-kotlin-stub:$grpc_kotlin_version")
    implementation("com.google.protobuf:protobuf-kotlin-lite:$protobuf_version")
}

android {
    compileSdk = 32
    buildToolsVersion = "32.0.0"

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs = ["-opt-in=kotlin.RequiresOptIn"]
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobuf_version"
    }
    plugins {
        java {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version"
        }
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version"
        }
        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:$grpc_kotlin_version:jdk7@jar"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                kotlin { option 'lite' }
            }
            task.plugins {
                java { option 'lite' }
                grpc { option 'lite' }
                grpckt { option 'lite' }
            }
        }
    }
}
